module "observability" {
  source = "./modules/observability"

  name_prefix    = local.name_prefix
  aws_region     = var.aws_region
  project_name   = var.project_name
  environment    = var.environment
  log_bucket_sse = true
  log_group_name = local.bedrock_log_group

  enable_bedrock_cost_budget          = var.enable_bedrock_cost_budget
  bedrock_monthly_budget_limit_usd    = var.bedrock_monthly_budget_limit_usd
  enable_bedrock_budget_slack_chatbot = var.enable_bedrock_budget_slack_chatbot
  bedrock_budget_slack_team_id        = var.bedrock_budget_slack_team_id
  bedrock_budget_slack_channel_id     = var.bedrock_budget_slack_channel_id
}

module "bedrock" {
  source = "./modules/bedrock"

  name_prefix                = local.name_prefix
  aws_region                 = var.aws_region
  project_name               = var.project_name
  environment                = var.environment
  model_invoke_resource_arns = var.model_invoke_resource_arns

  bedrock_logs_bucket_id = module.observability.bedrock_logs_bucket_id
  cloudwatch_kms_key_arn = module.observability.bedrock_logs_kms_key_arn
  log_group_name         = local.bedrock_log_group
  log_key_prefix         = "model-invocations"
  enable_guardrail       = var.enable_guardrail

  depends_on = [
    module.observability
  ]
}

module "litellm" {
  count  = var.enable_litellm_config_bucket ? 1 : 0
  source = "./modules/litellm"

  name_prefix                = local.name_prefix
  litellm_config_bucket_name = var.litellm_config_bucket_name
  tags                       = var.tags

  # Ensure log bucket / KMS exist first (shared account bootstrap ordering).
  depends_on = [module.observability]
}

module "iam" {
  source = "./modules/iam"

  name_prefix                    = local.name_prefix
  bedrock_invoke_policy_arn      = module.bedrock.bedrock_invoke_policy_arn
  litellm_s3_policy_arn          = var.enable_litellm_config_bucket ? module.litellm[0].config_bucket_policy_arn : null
  litellm_s3_attachments_enabled = var.enable_litellm_config_bucket
  teams                          = var.teams
  team_role_trust_principals     = var.team_role_trust_principals
  allow_account_root_trust       = var.allow_account_root_trust_principal
  iam_users                      = var.iam_users

  enable_cursor_bedrock_cross_account_role = local.enable_cursor_bedrock_cross_account_role
  cursor_cross_account_assumer_role_arn    = var.cursor_cross_account_assumer_role_arn
  cursor_bedrock_external_id               = var.cursor_bedrock_external_id
  cursor_bedrock_role_name_suffix          = var.cursor_bedrock_role_name_suffix

  # Ordering: implicit dependency on module.litellm when enable_litellm_config_bucket (litellm_s3_policy_arn).
  depends_on = [module.bedrock]
}

module "networking" {
  count  = var.enable_bedrock_private_endpoints ? 1 : 0
  source = "./modules/networking"

  name_prefix                     = local.name_prefix
  vpc_id                          = var.vpc_id
  private_subnet_ids              = var.private_subnet_ids
  aws_region                      = var.aws_region
  endpoint_allowed_principal_arns = var.endpoint_allowed_principal_arns
}

locals {
  name_prefix       = "${var.project_name}-${var.environment}"
  bedrock_log_group = "/aws/bedrock/model-invocations"

  enable_cursor_bedrock_cross_account_role = true
}
